{
module Lexer

open System
open System.Text
open FSharp.Text.Lexing
open Ast
open Comma.Errors
open Comma.ErrorLogger
open Parser

let lexeme (lexbuf:LexBuffer<_>) = Encoding.UTF8.GetString(lexbuf.Lexeme)
let setNextLine (lexbuf:LexBuffer<_>) = lexbuf.EndPos <- lexbuf.EndPos.NextLine

let tryInt32 lexeme =
    try Ok (LitInt (int lexeme))
    with _ -> Result.Error (integerOverflow, LitInt 0)

let tryFloat lexeme =
    try Ok (LitFloat (float lexeme))
    with _ -> Result.Error (invalidFloat, LitFloat 0.)
}

let digit = ['0'-'9']
let integer = '0'|['1'-'9']digit*
let alpha = ['a'-'z' 'A'-'Z']
let whitespace = [' ' '\t']
let newline = ('\n' | '\r' '\n')
let scientific = ['e''E']['+''-']? digit+
let fixed = '.' digit+
let float = integer (fixed | scientific | fixed scientific)
let id = ['_']* alpha (alpha | digit | '_')*

rule tokenize = parse
| "//"                      { singleLineComment lexbuf }	
| newline                   { setNextLine lexbuf; tokenize lexbuf }
| whitespace                { tokenize lexbuf }
| '"'                       { singleLineString (lexbuf.EndPos) (new StringBuilder()) lexbuf }
| integer                   { tryInt32 (lexeme lexbuf) }
| float                     { tryFloat (lexeme lexbuf) }
| "true"                    { Ok (LitBool true) }
| "false"                   { Ok (LitBool false) }
| '+'                       { Ok Plus }
| '-'                       { Ok Minus }
| '*'                       { Ok Asterisk }
| '/'                       { Ok LSlash }
| '='                       { Ok Eq }
| '>'                       { Ok Greater }
| '<'                       { Ok Less }
| ">="                      { Ok GreaterEq }
| "<="                      { Ok LessEq }
| "=="                      { Ok EqEq }
| "!="                      { Ok BangEq }
| "&&"                      { Ok AmpAmp }
| "||"                      { Ok BarBar }
| '!'                       { Ok Bang }
| '('                       { Ok LParen }
| ')'                       { Ok RParen }
| '{'                       { Ok LBrace }
| '}'                       { Ok RBrace }
| '['                       { Ok LBrack }
| ']'                       { Ok RBrack }
| ';'                       { Ok Semicolon }
| ':'                       { Ok Colon }
| "->"                      { Ok RArrow }
| '.'                       { Ok Dot }
| ','                       { Ok Comma }
| "while"                   { Ok While }
| "do"                      { Ok Do }
| "goto"                    { Ok GoTo }
| "break"                   { Ok Break }
| "continue"                { Ok Continue }
| "if"                      { Ok If }
| "else"                    { Ok Else }
| "func"                    { Ok Func }
| "return"                  { Ok Return }
| "var"                     { Ok Var }
| "struct"                  { Ok Struct }
| "new"                     { Ok New }
| id                        { Ok (Id (lexeme lexbuf)) }
| eof                       { Ok Eof }
| _                         { Result.Error (unknownIdentifier, LexFailure (lexeme lexbuf)) }

and singleLineComment = parse
| newline                   { setNextLine lexbuf; tokenize lexbuf }
| eof                       { Ok Eof }
| _                         { singleLineComment lexbuf }

and singleLineString pos state = parse
| '"'                       { lexbuf.StartPos <- pos; Ok (LitString (string state)) }
| newline                   { setNextLine lexbuf; Result.Error (unmatchedDoubleQuote, LitString (string state)) }
| eof                       { Result.Error (unmatchedDoubleQuote, Eof) }
| _                         { singleLineString pos (state.Append (lexeme lexbuf)) lexbuf }

{
let getToken (logger: Logger) (lexbuf: LexBuffer<_>) =
    match (tokenize lexbuf) with
    | Ok tok -> 
        trace logger (string tok); tok
    | Result.Error (text, tok) -> 
        Ast.handleError <| TaggedMessage (Lexing, (lexbuf.StartPos, lexbuf.EndPos), text)
        info logger (sprintf "[RECOVER] %s" (string tok)); tok
}